{-# LANGUAGE BangPatterns, OverloadedStrings #-}
module Data.Text.Serialize.Show(Show(..), showParen, showBuildable, showPrelude) where

import Data.Text.Serialize.Show.Class(Show(..))
import Data.Text.Serialize.Show.Generic()

import GHC.Generics

import qualified Prelude
import Prelude hiding(Show, show, showsPrec, showParen, showList)
import qualified Data.Text.Buildable as B
import Data.Int
import Data.Char(ord, isDigit)
import qualified Data.ByteString.Char8 as SB
import qualified Data.ByteString.Lazy.Char8 as LB
import qualified Data.Text as S
import qualified Data.Text.Lazy as L
import Data.Text.Lazy.Builder(Builder)
import Data.Array
import Data.Monoid
import Data.Word

-- just for instances
import qualified GHC.Generics
import qualified Data.Typeable

showParen :: Bool -> Builder -> Builder
showParen !b !p = (if b then "(" else mempty) <> p <> (if b then ")" else mempty)
{-# INLINABLE showParen #-}

showBuildable :: B.Buildable a => Int -> a -> Builder
showBuildable _prec a = B.build a
{-# INLINABLE showBuildable #-}

showPrelude :: Prelude.Show a => Int -> a -> Builder
showPrelude n a = B.build (Prelude.showsPrec n a "")
{-# INLINABLE showPrelude #-}

-- Numeric instances (from Data.Text.Buildable)
instance Show Double  where showPrec = showBuildable
instance Show Float   where showPrec = showBuildable
instance Show Int     where showPrec = showBuildable
instance Show Int8    where showPrec = showBuildable
instance Show Int16   where showPrec = showBuildable
instance Show Int32   where showPrec = showBuildable
instance Show Int64   where showPrec = showBuildable
instance Show Integer where showPrec = showBuildable
instance Show Word    where showPrec = showBuildable
instance Show Word8   where showPrec = showBuildable
instance Show Word16  where showPrec = showBuildable
instance Show Word32  where showPrec = showBuildable
instance Show Word64  where showPrec = showBuildable

-- Custom string-like instances
instance Show Char where
  showPrec _ '\'' = "'\\''"
  showPrec _ c = "\'" <> litChar c <> "\'"
  {-# INLINABLE showPrec #-}

  showList str = "\"" <> litText (L.pack str) <> "\""
  {-# INLINABLE showList #-}
instance Show L.Text where
  showPrec _ t = "\"" <> litText t <> "\""
  {-# INLINABLE showPrec #-}
instance Show S.Text where
  showPrec _ t = "\"" <> litText (L.fromStrict t) <> "\""
  {-# INLINABLE showPrec #-}
instance Show SB.ByteString where
  showPrec _ b = "\"" <> litText (L.pack (SB.unpack b)) <> "\""
  {-# INLINABLE showPrec #-}
instance Show LB.ByteString where
  showPrec _ b = "\"" <> litText (L.pack (LB.unpack b)) <> "\""
  {-# INLINABLE showPrec #-}

-- List instance
instance Show a => Show [a] where
  showPrec _ as = showList as
  {-# INLINABLE showPrec #-}

-- Escaping code (for Char and Text)
litText :: L.Text -> Builder
litText t = 
  case L.break isEscape t of
    (l, r) -> case L.uncons r of
      Nothing -> B.build l
      Just (c, r') -> case L.uncons r' of
        Nothing -> B.build l <> escape c
        Just (c', _) -> if needsEmptyChar c c'
                        then B.build l <> escape c <> "\\&" <> litText r'
                        else B.build l <> escape c <> litText r'
  where
    isEscape :: Char -> Bool
    isEscape c = c == '\\' || c == '"' || c < ' ' || c >= '\DEL'
    
    escape :: Char -> Builder
    escape '\\' = "\\\\"
    escape '"' = "\\\""
    escape '\DEL' = "\\DEL"
    escape c | c > '\DEL' = "\\" <> B.build (ord c)
             | otherwise = "\\" <> B.build (escapes ! ord c)

    needsEmptyChar :: Char -> Char -> Bool
    needsEmptyChar '\SOH' 'H' = True
    needsEmptyChar c d = c > '\DEL' && isDigit d

-- Generated by:
-- @map (init . drop 2 . show . chr) [0..ord ' '-1]@
escapes :: Array Int S.Text
escapes = listArray (0,ord ' ')
          ["NUL","SOH","STX","ETX","EOT","ENQ","ACK","a","b","t","n","v","f","r","SO","SI","DLE",
           "DC1","DC2","DC3","DC4","NAK","SYN","ETB","CAN","EM","SUB","ESC","FS","GS","RS","US"]

litChar :: Char -> Builder
litChar '\\' = "\\\\"
litChar '\'' = "\\'"
litChar '\DEL' = "\\DEL"
litChar c | c > '\DEL' = "\\" <> B.build (ord c)
          | c >= ' ' = B.build c
          | otherwise = "\\" <> B.build (escapes ! ord c)

-- Custom tuple instances
instance Show () where showPrec _ () = "()"
instance (Show a, Show b) => Show (a, b) where 
  showPrec _ (a, b) = "(" <> show a <> "," <> show b <> ")"
  {-# INLINABLE showPrec #-}
instance (Show a, Show b, Show c) => Show (a, b, c) where 
  showPrec _ (a, b, c) = "(" <> show a <> "," <> show b <> "," <> show c <> ")"
  {-# INLINABLE showPrec #-}
instance (Show a, Show b, Show c, Show d) => Show (a, b, c, d) where 
  showPrec _ (a, b, c, d) = "(" <> show a <> "," <> show b <> "," <> show c <> "," <> show d <> ")"
  {-# INLINABLE showPrec #-}
instance (Show a, Show b, Show c, Show d, Show e) => Show (a, b, c, d, e) where 
  showPrec _ (a, b, c, d, e) = "(" <> show a <> "," <> show b <> "," <> show c <> "," <> show d <> "," <> show e <> ")"
  {-# INLINABLE showPrec #-}
instance (Show a, Show b, Show c, Show d, Show e, Show f) => Show (a, b, c, d, e, f) where 
  showPrec _ (a, b, c, d, e, f) = "(" <> show a <> "," <> show b <> "," <> show c <> "," <> show d <> "," <> show e <> "," <> show f <> ")"
  {-# INLINABLE showPrec #-}
instance (Show a, Show b, Show c, Show d, Show e, Show f, Show g) => Show (a, b, c, d, e, f, g) where 
  showPrec _ (a, b, c, d, e, f, g) = "(" <> show a <> "," <> show b <> "," <> show c <> "," <> show d <> "," <> show e <> "," <> show f <> "," <> show g <> ")"
  {-# INLINABLE showPrec #-}
instance (Show a, Show b, Show c, Show d, Show e, Show f, Show g, Show h) => Show (a, b, c, d, e, f, g, h) where 
  showPrec _ (a, b, c, d, e, f, g, h) = "(" <> show a <> "," <> show b <> "," <> show c <> "," <> show d <> "," <> show e <> "," <> show f <> "," <> show g <> "," <> show h <> ")"
  {-# INLINABLE showPrec #-}
instance (Show a, Show b, Show c, Show d, Show e, Show f, Show g, Show h, Show i) => Show (a, b, c, d, e, f, g, h, i) where 
  showPrec _ (a, b, c, d, e, f, g, h, i) = "(" <> show a <> "," <> show b <> "," <> show c <> "," <> show d <> "," <> show e <> "," <> show f <> "," <> show g <> "," <> show h <> "," <> show i <> ")"
  {-# INLINABLE showPrec #-}
{-
instance (Show a, Show b, Show c, Show ) => Show (a, b, c) where 
  showPrec _ (a, b, c) = "(" <> show a <> "," <> show b <> "," <> show c <> "," <> show d <> "," <> show e <> "," <> show f <> "," <> show g <> "," <> show h <> "," <> show i <> "," <> show j <> ")"
instance (Show a, Show b, Show c) => Show (a, b, c) where 
  showPrec _ (a, b, c) = "(" <> show a <> "," <> show b <> "," <> show c <> "," <> show d <> "," <> show e <> "," <> show f <> "," <> show g <> "," <> show h <> "," <> show i <> "," <> show j <> "," <> show k <> ")"
instance (Show a, Show b, Show c) => Show (a, b, c) where 
  showPrec _ (a, b, c) = "(" <> show a <> "," <> show b <> "," <> show c <> "," <> show d <> "," <> show e <> "," <> show f <> "," <> show g <> "," <> show h <> "," <> show i <> "," <> show j <> "," <> show k <> "," <> show l <> ")"
instance (Show a, Show b, Show c) => Show (a, b, c) where 
  showPrec _ (a, b, c) = "(" <> show a <> "," <> show b <> "," <> show c <> "," <> show d <> "," <> show e <> "," <> show f <> "," <> show g <> "," <> show h <> "," <> show i <> "," <> show j <> "," <> show k <> "," <> show l <> "," <> show m <> ")"
instance (Show a, Show b, Show c) => Show (a, b, c) where 
  showPrec _ (a, b, c) = "(" <> show a <> "," <> show b <> "," <> show c <> "," <> show d <> "," <> show e <> "," <> show f <> "," <> show g <> "," <> show h <> "," <> show i <> "," <> show j <> "," <> show k <> "," <> show l <> "," <> show m <> "," <> show n <> ")"
instance (Show a, Show b, Show c) => Show (a, b, c) where 
  showPrec _ (a, b, c) = "(" <> show a <> "," <> show b <> "," <> show c <> "," <> show d <> "," <> show e <> "," <> show f <> "," <> show g <> "," <> show h <> "," <> show i <> "," <> show j <> "," <> show k <> "," <> show l <> "," <> show m <> "," <> show n <> "," <> show o <> ")"
-}                                               

-- Prelude instances
instance Show GHC.Generics.Arity         where showPrec = showPrelude
instance Show GHC.Generics.Fixity        where showPrec = showPrelude
instance Show GHC.Generics.Associativity where showPrec = showPrelude
instance Show Data.Typeable.TyCon        where showPrec = showPrelude
instance Show Data.Typeable.TypeRep      where showPrec = showPrelude

-- Generic instances
instance Show Bool
instance Show Ordering
instance (Show a, Show b) => Show (Either a b)
